buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.5"
}

group = 'com.yongbeom.aircalendar'
version = '0.0.1'

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }

    apply plugin: 'maven-publish'
    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'
}

publishing {
    publications {
        MySDK(MavenPublication) {
            groupId 'com.yongbeom.aircalendar'
            version "${version}"
        }
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "${version}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.0.2'
    compile 'com.android.support:recyclerview-v7:26.0.2'
    compile 'com.android.support:design:26.0.2'
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'YongBeomLee'
            description 'Airbnb-style Android Calendar'
            url 'https://github.com/yongbeam/AirCalendar'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'com.yongbeom.aircalendar'
            artifactId 'AirCalendar'
            version "${version}"

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://mit-license.org'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/yongbeam/AirCalendar.git'
                url 'https://github.com/yongbeam/AirCalendar'

            }
            developers {
                developer {
                    name 'YongBeomLee'
                }
            }
        }
    }
}

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'AirCalendar'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/yongbeam/AirCalendar'
        publish = true
        publicDownloadNumbers = true
        version {
            name = "${version}"
            desc = 'Airbnb-style Android Calendar'
            vcsTag = "${version}"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}